script_enemy_main{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\index.dnh"
	let imgBoss=dotBossSample;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetText("＊符「＊＊＊」");
		SetScore(100000);
		SetTimer(68);
		SetShotAutoDeleteClip(20,20,20,20);
		SetEnemyMarker(true);

		// ボスステータス設定（ライフ、ダメージ率Shot%,Bomb%等）
		SetLife(500);
		SetDamageRate(15,10);

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（A:自弾,B:体当たり）
		SetCollisionA(GetX,GetY,32);
		SetCollisionB(GetX,GetY,24);

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(242);
		wait(2);

		// 初期移動
		let fr=120;
		let x=cenX;
		let y=minY+140;
		SetMovePosition02(x,y,fr);
		wait(fr);

		Concentration01(120);
		wait(120);

		// 花弾
		FlowerShot(-1);
		FlowerShot(1);

	}

	// 花弾 --------------------------------------------------------------------
	task FlowerShot(let assign){

		let way=4+2*Lv;
		let fr1=60;
		let speed1=4;
		let accSpeed1=-speed1/fr1;
		let speed2=2;
		let speed3=1.2;
		let wayAngle1=360/way;
		let baseAngle1=90-wayAngle1/2;
		let addBaseAngle1=360/480*assign;
		let angle2=-165*assign;
		let graphic=[RED12,ORANGE12,YELLOW12,GREEN12,AQUA12,BLUE12,PURPLE12];
		let gra=0;
		let delay=10;
		let shotFr=[17,17,13,11];

		loop{
			let gra_=trunc(gra);
			SetShotDirectionType(ABSOLUTE);
			ascent(let i in 0 .. way){
				let angle1=baseAngle1+wayAngle1*i;
				CreateShotA(0,GetX,GetY,delay);
				SetShotDataA(0,0,speed1,angle1,0,accSpeed1,0,graphic[gra_]);
				SetShotDataA(0,fr1,speed2,angle1+angle2,0,0,0,graphic[gra_]);
				CreateShotA(1,0,0,0);
				SetShotDataA(1,0,speed3,angle1+angle2-180,0,0,0,graphic[gra_]);
				AddShot(fr1,0,1,0);
				FireShot(0);
			}
			wait(shotFr[Lv]);
			baseAngle1+=addBaseAngle1*shotFr[Lv];
			gra+=1/5;
			if(gra>=7){gra=0;}
		}

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		SetTexture(imgBoss);
		if(GetSpeed==0){SetGraphicRect(1,1,64,64);}
		else if(GetAngle>-90&&GetAngle<=90){SetGraphicRect(192,1,255,64);}
		else {SetGraphicRect(128,1,191,64);}
		SetAlpha(255);
		SetColor(255,255,255);
		DrawGraphic(GetX,GetY);

	}

	// 共通関数読み込み
	#include_function ".\INCLUDE\patternShot.dnh"
	#include_function ".\INCLUDE\library_request.dnh"

}
