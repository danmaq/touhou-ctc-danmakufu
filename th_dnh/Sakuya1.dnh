#東方弾幕風
#Title[Sakuya - 通常攻撃1]
#Text[ステージ2A 中ボス
十六夜 咲夜

下記以外のスタート＝Easy
スキップ＋スタート＝Normal
ユーザー＋スタート＝Hard
ｽｷｯﾌﾟ＋ﾕｰｻﾞｰ＋ｽﾀｰﾄ＝Lunatic]
#Image[]
#BackGround[Default]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Enemy.dnh"

	// 画像及び効果音ファイル
	let imgBoss=dotBossSakuya;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetTimer(35);
		SetShotAutoDeleteClip(64,64,64,20);
		SetEnemyMarker(true);

		// ボスステータス設定（ライフ、ダメージ率等）
		SetLife(2500);
		SetDamageRate(80,40);
		SetX(cenX);
		SetY(minY+120+RandBlur(1));

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（自弾,体当たり）
		Collision(32,24);

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(62);
		_Wait(2);

		// 発動
		ExplosionBoss;
		_Wait(60);

		let assign=1;

		loop{

			// 側面反射弾
			SideReflectShot(assign);
			_Wait(150);

			// 移動
			move(60);
			_Wait(60);

			assign*=-1;

		}

	}

	// 側面反射弾 --------------------------------------------------------------
	task SideReflectShot(let assign){

		let way=[21,29,29,37];
		let num=[3,3,4,5];
		let baseSpeed=1;
		let maxSpeed=3;
		let numSpeed=(maxSpeed-baseSpeed)/(num[Lv]-1);
		let baseAngle=90-45*assign;
		let wayAngle=180/(way[Lv]-1)*assign;
		let delay=15;
		let reflect=1;
		let graphic=BLUE32;
		let L=true;
		let U=false;
		let R=false;
		let D=false;
		if(assign==-1){
			graphic=RED32;
			L=false;
			R=true;
		}
		if(Lv>=2){
			U=true;
		}
		let fr=1;

		ascent(let i in 0 .. way[Lv]){
			let angle=baseAngle+wayAngle*i;
			ascent(let j in 0 .. num[Lv]){
				let speed=baseSpeed+numSpeed*j;
				ReflectShot(GetX,GetY,speed,angle,reflect,L,U,R,D,graphic,delay);
			}
			_Wait(fr);
		}

	}

	// 移動 --------------------------------------------------------------------
	function move(let fr){

		let x=GetX;
		let y=GetY;
		let mx=rand(40,80);
		let my=rand(0,20);
		if(x>=GetPlayerX){
			if(x-mx>=minX+100){mx=-mx;}
		}
		else{
			if(x+mx>=maxX-100){mx=-mx;}
		}
		if(y>=minY+120){my=-my;}
		x+=mx;
		y+=my;
		let power=GetGapLength(GetX,GetY,x,y)*2;
		let angle=GetGapAngle(GetX,GetY,x,y);
		SetMovePositionHermite(x,y,power,angle,0,0,fr);

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		let x=0;
		let y=0;
		let sLength=128;
		SetAlpha(255);
		SetColor(255,255,255);
		_DrawChrRectSqr(imgBoss,GetX,GetY,x,y,sLength);

	}

}
