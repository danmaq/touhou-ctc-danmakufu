script_enemy_main{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Boss.dnh"

	// 画像及び効果音ファイル
	let imgBoss=dotBossMeirin;
	let imgCutInEnemy=cutMeirin;
	let imgCutInPlayer=cutMarisa;
	let imgList=[imgBoss,imgCutInEnemy,imgCutInPlayer];
	let seList=[];

	// 各種設定値
	let THC=GetCommonDataDefault(CD_THC_STAGE,false);
	Debug_SetLevel;

	// -------------------------------------------------------------------------
	@Initialize{

		// ボス初期設定（ファイルのロード他）
		if(!THC){seList=seList~seListEnemy;}
		InitializeBoss(imgList,seList);
		DeleteEnemyShotToItem(ALL);

		// スペルカード設定
		SetLife(1);
		SetDamageRate(0,0);
		SetX(maxX);
		SetY(minY-128);

		// ボス基本処理、メイン処理
		BehaviorBoss(ACT_STAND3);
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（自弾,体当たり）
		Collision(32,24);

		// ダメージ音
		PlayDamageSE(seDamage);

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		// 会話開始
		CreateEventFromScript("Talk");
		SetCommonData(CD_STAGE_EVENT,true);
		move(cenX,minY+120+RandBlur(0.1),60);
		while(GetEventStep!=1){yield;}

		// BGM
		DeleteMusic( bgmList[ 2 ] );
		_PlayMusicEx( bgmList[ 3 ], "" );
		let bgmCount=m_nCount;
		while(OnEvent||m_nCount-bgmCount<=30){yield;}
		DeleteCommonData(CD_STAGE_EVENT);

		AddLife(-1);

	}


	// 移動 --------------------------------------------------------------------
	function move(let x, let y, let fr){

		let power=GetGapLength(GetX,GetY,x,y)*2;
		let angle=GetGapAngle(GetX,GetY,x,y);
		SetMovePositionHermite(x,y,power,angle,0,0,fr);
		SetAction(ACT_MOVE,fr);

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphicList(imgList,seList);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		DrawBoss(imgBoss);

	}

}

// 会話 ------------------------------------------------------------------------
script_event Talk{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Event.dnh"

	// 画像及び効果音ファイル
	let imgCutInEnemy=cutMeirin;
	let imgCutInPlayer=cutMarisa;

	// -------------------------------------------------------------------------
	@Initialize{

		// メイン処理
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		TextOutB(90,"\c[RED]妹様ぁー");
		ChrIn(imgCutInEnemy,RIGHT,CUT_NORMAL);
		SetNameFromText(RIGHT,TEXT_RED,"紅 美鈴","紅魔館の門番");
		Enemy;
		TextOut("\c[RED]すみません妹様見ませんでした？");
		ChrIn(imgCutInPlayer,LEFT,CUT_NORMAL);
		Player;
		TextOut("\c[BLUE]お前妹なんて居たのか？");
		Enemy;
		TextOut("\c[RED]お嬢様の妹ですよ！");
		TextOut("\c[RED]私や咲夜様の目を盗んで館から出てきてしまったんですよ〜");
		Player;
		TextOut("\c[BLUE]この道をずーっと進んでみな、お前の望むものが転がってるぜ");
		SetStep(1);
		Enemy;
		TextOut("\c[RED]…どうやら私は妹様の仇をとらなければいけないようですね");
		Player;
		TextOut("\c[BLUE]と言うことで、私は先を急ぐんで");
		Enemy;
		TextOut("\c[RED]通せるわけないでしょ！");
		End;

	}

}
