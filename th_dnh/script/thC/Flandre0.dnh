script_enemy_main{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Boss.dnh"

	// 画像及び効果音ファイル
	let imgBoss=dotBossFlandre;
	let imgList=[imgBoss];
	let seList=[];

	// 各種設定値
	let THC=GetCommonDataDefault(CD_THC_STAGE,false);
	Debug_SetLevel;

	// -------------------------------------------------------------------------
	@Initialize{

		// ボス初期設定（ファイルのロード他）
		if(!THC){
			seList=seList~seListEnemy;
		}
		InitializeBoss(imgList,seList);
		DeleteEnemyShotToItem(ALL);

		// スペルカード設定
		SetLife(1);
		SetDamageRate(0,0);
		SetX(minX-128);
		SetY(minY+128);

		// ボス基本処理、メイン処理
		BehaviorBoss(ACT_STAND4);
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（自弾,体当たり）
		Collision(32,24);

		// ダメージ音
		PlayDamageSE(seDamage);

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		let x=cenX;
		let y=minY+120;

		SetAction(ACT_MOVE_R,120);
		move((GetX+x)/2,(GetY+y)/2,60,1);
		_Wait(60);
		move(x,y,60,-1);
		_Wait(60);

		AddLife(-1);

	}


	// 移動 --------------------------------------------------------------------
	function move(let x, let y, let fr, let assign){

		let powerS=200;
		let angleS=GetGapAngle(GetX,GetY,x,y)-60*assign;
		let powerE=0;
		if(assign==1){powerE=powerS;}
		let angleE=GetGapAngle(GetX,GetY,x,y)+60*assign;

		SetMovePositionHermite(x,y,powerS,angleS,powerE,angleE,fr);

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphicList(imgList,seList);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		DrawBoss(imgBoss);

	}

}
