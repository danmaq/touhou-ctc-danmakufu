script_enemy_main{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Boss.dnh"
	#include_function ".\_index_UserShot.dnh"

	// 画像及び効果音ファイル
	let userShot=shotAll;
	let imgBoss=dotBossAya;
	let imgShot=dotShotTengu;
	let imgShadow=dotCircle6s;
	let imgList=[imgBoss,imgShot];
	let seList=[];

	// 各種設定値（難易度等）
	let THC=GetCommonDataDefault(CD_THC_STAGE,false);
	let Practice=GetCommonDataDefault(CD_STAGE_PRACTICE,false); 
	Debug_SetLevel;
	let Lv=GetCommonDataDefault(CD_STAGE_LEVEL,0);

	// -------------------------------------------------------------------------
	@Initialize{

		// ボス初期設定（ファイルのロード他）
		if(!THC){
			LoadUserShotData(userShot);
			imgList=imgList~[imgShadow];
			seList=seList~seListEnemy~seListShadow;
			SetX(cenX);
			SetY(minY+120+RandBlur(0.1));
		}
		InitializeBoss(imgList,seList);

		// スペルカード設定
		SetTimer(45);
		SetLife(2500);
		SetDamageRate(40,40);
		SetShotAutoDeleteClip(32,32,32,32);

		// ボス基本処理、メイン処理
		CountTimer(seTimeOut);
		BehaviorBoss(ACT_STAND3);
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（自弾,体当たり）
		Collision(32,24);

		// ダメージ音
		PlayDamageSE(seDamage);

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(62);
		_Wait(62);

		// 使い魔召喚
		Concentration01(60);
		_Wait(60);
		ascent(let i in 0 .. 1+Lv){
			CreateEnemyFromScript("Shadow",GetX,GetY,0,0,i);
		}
		PlaySE(seShadow);
		_Wait(60);
		SetDamageRate(90,90);

		loop{

			// 全方向弾
			SetAction(ACT_SHOT_A3,150);
			loop(2){
				_RoundShot;
				_Wait(60);
			}

			// 移動
			move(30);
			_Wait(60);

		}

	}

	// 全方向弾 ----------------------------------------------------------------
	task _RoundShot{

		let num=2+Lv;
		let way=28+4*Lv;
		let speed=4+0.4*Lv;
		let angle=90+360/way/2;
		let graphic=[US_BUTTERFLY_RED,US_BUTTERFLY_BLUE];
		let delay=15;
		let fr=5;

		ascent(let i in 0 .. num){
			RoundShot01(GetX,GetY,speed,angle,way,graphic[i%2],delay);
			PlaySE(seShot1);
			_Wait(fr);
		}

	}

	// 移動 --------------------------------------------------------------------
	task move(let fr){

		let x=GetX;
		let y=GetY;
		let mx=rand(40,80);
		let my=rand(0,20);
		if(x>=GetPlayerX){
			if(x-mx>=minX+100){mx=-mx;}
		}
		else{
			if(x+mx>=maxX-100){mx=-mx;}
		}
		if(y>=minY+120){my=-my;}
		x+=mx;
		y+=my;
		SetMovePosition02(x,y,fr);
		SetAction(ACT_MOVE,fr);

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphicList(imgList,seList);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		DrawBoss(imgBoss);

	}

}

// 使い魔 ----------------------------------------------------------------------
script_enemy Shadow{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Shadow.dnh"

	// 画像及び効果音ファイル
	let imgShadow=dotCircle6s;

	// 各種設定値（難易度等）
	let THC=GetCommonDataDefault(CD_THC_STAGE,false);
	let Practice=GetCommonDataDefault(CD_STAGE_PRACTICE,false); 
	let Lv=GetCommonDataDefault(CD_STAGE_LEVEL,0);

	let n=GetArgument;

	// -------------------------------------------------------------------------
	@Initialize{

		// ステータス設定（ライフ、ダメージ率等）
		SetLife(1);
		SetDamageRateEx(0,0,0,0);

		// 実体化時の光
		LightShadow;

		// メイン処理
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		// 位置設定
		setLocation;
		loop(100+15*n){yield;}

		loop{
			// 天狗弾
			let speed=-2;
			let angle=GetAngleToPlayer;
			CreateShotFromScript("TenguShot",GetX,GetY,speed,angle,0,0);
			PlaySE(seShot2);
			loop(80){yield;}
		}

	}

	// 位置設定 ----------------------------------------------------------------
	task setLocation{

		let xTurnAngle=20;
		let zTurnAngle=0;
		let gapLength=0;
		let gapAngle=360/(Lv+1)*n;
		setGapLength;
		setGapAngle;
		setZTurnAngle;

		// 位置設定 ----------------------------------------
		loop{
			let x=GetGapX(GetEnemyX,gapLength,gapAngle);
			let y=GetGapY(GetEnemyY,gapLength*sin(xTurnAngle),gapAngle);
			let _gapLength=GetGapLength(GetEnemyX,GetEnemyY,x,y);
			let _gapAngle=GetGapAngle(GetEnemyX,GetEnemyY,x,y)+zTurnAngle;
			let _x=GetGapX(GetEnemyX,_gapLength,_gapAngle);
			let _y=GetGapY(GetEnemyY,_gapLength,_gapAngle);
			SetX(_x);
			SetY(_y);
			yield;
		}

		// 間隔長さ設定 ------------------------------------
		task setGapLength{
			let fr=60;
			let addGapLength=90/fr;
			loop(fr){
				gapLength+=addGapLength;
				yield;
			}
		}

		// 間隔角度設定 ------------------------------------
		task setGapAngle{
			let addGapAngle=-360/120;
			loop{
				gapAngle+=addGapAngle;
				yield;
			}
		}

		// z軸回転角度設定 ---------------------------------
		task setZTurnAngle{
			let addZTurnAngle=-360/900;
			loop{
				zTurnAngle+=addZTurnAngle;
				yield;
			}
		}

	}

	// -------------------------------------------------------------------------
	@Finalize{

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// 本体描画
		let shadow=SHADOW_S_WHITE;
		let angle=2*m_nCount;
		let scale=0.8;
		_DrawShadow(shadow,imgShadow,angle,scale,scale);

	}

}

// 天狗弾 ----------------------------------------------------------------------
script_shot TenguShot{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Shot.dnh"
	#include_function ".\_index_UserShot.dnh"

	// 画像及び効果音ファイル
	let imgShot=dotShotTengu;

	// 各種設定値（難易度等）
	let THC=GetCommonDataDefault(CD_THC_STAGE,false);
	let Practice=GetCommonDataDefault(CD_STAGE_PRACTICE,false); 
	let Lv=GetCommonDataDefault(CD_STAGE_LEVEL,0);

	let scaleY=0;
	let draw=true;
	let alive=true;
	let objB=[];

	// -------------------------------------------------------------------------
	@Initialize{

		SetBombResist;
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 画面外で消滅待ち
		if(IsOut(64)){alive=false;}

		// 当たり判定
		ascent(let i in 0 .. length(objB)){
			SetCollisionB(Obj_GetX(objB[i]),Obj_GetY(objB[i]),13);
		}

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		// 速度設定
		setSpeed;

		// 反射チェック
		reflect;

		// 拡大率設定
		setScale;

		// 残像
		while(GetSpeed<0){yield;}
		draw=false;
		while(alive){
			Blur;
			yield;
		}

		// 残像消滅待ち
		loop(15){yield;}
		End;

	}

	// 速度設定 ----------------------------------------------------------------
	task setSpeed{

		let speed=GetSpeed;
		let addSpeed=(8.5+Lv-speed)/60;
		loop{
			speed+=addSpeed;
			SetSpeed(speed);
			yield;
		}

	}

	// 反射チェック ------------------------------------------------------------
	task reflect{

		let angle=GetAngle;
		loop{
			if(GetY<=minY){
				angle=-angle;
				break;
			}
			if(GetX<=minX||GetX>=maxX){
				angle=180-angle;
				break;
			}
			yield;
		}
		SetAngle(angle);

	}

	// 拡大率設定 --------------------------------------------------------------
	task setScale{

		let fr=15;
		let addScaleY=1/fr;
		loop(fr){
			scaleY+=addScaleY;
			yield;
		}

	}

	// 残像 --------------------------------------------------------------------
	task Blur{

		// 初期設定 ----------------------------------------
		let obj=Obj_Create(OBJ_SHOT);
		objB=objB~[obj];

		Obj_SetX(obj,GetX);
		Obj_SetY(obj,GetY);
		Obj_SetSpeed(obj,0);
		Obj_SetAngle(obj,GetAngle);
		ObjShot_SetGraphic(obj,US_TENGU);
		Obj_SetCollisionToPlayer(obj,false);
		ObjShot_SetBombResist(obj,true);

		// メイン処理 --------------------------------------
		loop(6){yield;}
		objB=erase(objB,0);
		Obj_Delete(obj);

	}

	// -------------------------------------------------------------------------
	@Finalize{

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// 弾描画
		if(draw){
			SetGraphicRect(0,0,128,32);
			SetRenderState(ADD);
			SetColor(255,255,255);
			SetGraphicAngle(0,0,GetAngle);
			SetGraphicScale(1,scaleY);
			SetTexture(imgShot);
			DrawGraphic(GetX,GetY);
		}

	}

}
