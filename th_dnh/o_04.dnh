script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"


//変数宣言--------------------------------------
	let count=0;
	let Level = GetCommonDataDefault( "Level", 1 );
	SetCommonData("limit",[200,150,100,70][Level]);

//タスクの作成----------------------------------

task MainAction()
{
	yield;
	Concentration01(80);
	_Wait(80);
	SetCommonData("SetX",GetCenterX());
	SetCommonData("SetY",GetCenterY());
	let angle=-180;
	while(angle<180)
	{
		CreateEnemyFromScript("t_ma_01",GetX(),GetY(),2,angle,0);
		angle+=12;
	}
	_Wait(500);
	ascent(i in 0..300)
	{
		SetCommonData("SetX",Smooth(GetCenterX(),GetCenterX()/2,i,300));
		SetCommonData("SetY",Smooth(GetCenterY(),GetCenterY()*1.5,i,300));
		yield;
	}
	_Wait(200);
	ascent(i in 0..300)
	{
		SetCommonData("SetX",Smooth(GetCenterX()/2,GetCenterX()*1.5,i,300));
		yield;
	}
	_Wait(200);
	ascent(i in 0..200)
	{
		SetCommonData("limit",Smooth([200,150,100,70][Level],[200,150,100,70][Level]/2,i,200));
		yield;
	}
	ascent(i in 0..200)
	{
		SetCommonData("limit",Smooth([200,150,100,70][Level]/2,[200,150,100,70][Level]*0.66,i,200));
		yield;
	}
	_Wait(200);
	ascent(i in 0..300)
	{
		SetCommonData("SetX",Smooth(GetCenterX()*1.5,GetCenterX(),i,300));
		SetCommonData("SetY",Smooth(GetCenterY()*1.5,GetCenterY(),i,300));
		yield;
	}
	//計約2000fr
}


//実行部分---------------------------------------

	@Initialize
	{
		SetLife(1);
		MainAction();
	}
	@MainLoop
	{
		Deletemine();
		yield;
		count++;

	}
	@DrawLoop
	{}
	@Finalize
	{}
}



////////////////////////////////////////////////////////////////////////
//仮の使い魔、使い魔の書式の統一が必要なので雛形の作成をお願いします
////////////////////////////////////////////////////////////////////////

script_enemy t_ma_01
{

#include_function ".\INCLUDE\Shadow.dnh"
#include_function ".\index_UserShot.dnh"

	let Level=GetCommonData("Level");
	let spmax=GetSpeed();
	let count=0;
	let angle=GetAngle();
	let limit=GetCommonData("limit");

task MainAction()
{
	yield;
	_Wait(300);
	let i=int(rand(0,80));
	while(1)
	{
		CreateShot01(GetX(),GetY(),5,angle+Smooth(-90,90,i,100),192,10);
		CreateShot01(GetX(),GetY(),5,angle+Smooth(90,-90,i,100),192,10);
		i++;
		if(i==101){i=0;}
		loop(5){yield;}
	}
}

task SubAction()
{
	yield;
	SetMovePositionHermite(GetCenterX()+limit*cos(angle),GetCenterY()+limit*sin(angle),200,0,300,angle,200);
	_Wait(200);
	while(1)
	{
		SetX(GetCommonData("SetX")+limit*cos(angle));
		SetY(GetCommonData("SetY")+limit*sin(angle));
		angle+=4.1;
		yield;
	}
}

	@Initialize
	{	
		SetLife(1);
		LoadGraphic(dotCircle6s);
		MainAction();
		SubAction();
		LoadUserShotData(shotAll);
	}
	@MainLoop
	{
		yield;
		Deletemine();
		count++;
		limit=GetCommonData("limit");
	}
	@DrawLoop
	{
		SetTexture(dotCircle6s);
		SetGraphicRect(0,0,64,64);
		SetGraphicScale(0.8,0.8);
		SetColor(255,255,255);
		SetGraphicAngle(0,0,count*2);
		DrawGraphic(GetX,GetY);
	}
	@Finalize
	{
		DestructShadow( 40 );
		CreateItem(ITEM_SCORE,GetX(),GetY());
	}
}
