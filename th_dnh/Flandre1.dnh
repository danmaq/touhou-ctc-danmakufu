#東方弾幕風
#Title[Flandre - 通常攻撃1]
#Text[ステージ1A 中ボス
フランドール・スカーレット

下記以外のスタート＝Easy
スキップ＋スタート＝Normal
ユーザー＋スタート＝Hard
ｽｷｯﾌﾟ＋ﾕｰｻﾞｰ＋ｽﾀｰﾄ＝Lunatic]
#Image[]
#BackGround[Default]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main{

	// 共通関数読み込み
	#include_function ".\INCLUDE\Enemy.dnh"

	// 画像及び効果音ファイル
	let imgBoss=dotBossFlandre;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetTimer(37);
		SetShotAutoDeleteClip(20,20,20,20);
		SetEnemyMarker(true);

		// ボスステータス設定（ライフ、ダメージ率等）
		SetLife(2500);
		SetDamageRate(80,40);
		SetX(cenX);
		SetY(minY+120);

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（自弾,体当たり）
		Collision(32,24);

		// カウンタ加算
		m_nCount++;

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(62);
		_Wait(2);

		// 発動
		ExplosionBoss;
		_Wait(60);

		let turn=0;

		loop{

			// 全方向直線弾1
			RoundLineShot1(turn);
			_Wait(90);

			// 移動
			move(cenX+120,minY+180,90);
			_Wait(90);

			// 全方向扇状直線弾1
			RoundWideLineShot1;
			_Wait(90);

			// 移動
			move(cenX,minY+120,90);
			_Wait(90);

			if(turn<2){turn++;}

			// 全方向直線弾1
			RoundLineShot1(turn);
			_Wait(90);

			// 移動
			move(cenX-120,minY+180,90);
			_Wait(90);

			// 全方向扇状直線弾1
			RoundWideLineShot1;
			_Wait(90);

			// 移動
			move(cenX,minY+120,90);
			_Wait(90);

			if(turn<2){turn++;}

		}

	}

	// 全方向直線弾1 -----------------------------------------------------------
	task RoundLineShot1(let turn){

		let num_=1+turn;
		let num=[1,2,3,4];
		let way=[34,34,40,46];
		let minSpeed=[1.8, 1.5, 1.2, 0.9];
		let maxSpeed=1.8;
		let graphic=BLUE12;
		let delay=15;
		let fr=30;

		ascent(let i in 0 .. num_){
			let angle=rand(0,360);
			SetShotDirectionType(ABSOLUTE);
			RoundLineShot(GetX,GetY,minSpeed[Lv],maxSpeed,angle,way[Lv],num[Lv],graphic,delay);
			_Wait(fr);
		}

	}

	// 全方向扇状直線弾1 -------------------------------------------------------
	task RoundWideLineShot1{

		let num_=[3,4,5,6];
		let num=[5,7,9,11];
		let wideAngle=[0,0,6,12];
		let wideWay=[1,1,3,5];
		let roundWay=5;
		let minSpeed=[1, 1.2, 1.4, 1.6];
		let maxSpeed=[3.4, 3.6, 4, 5];
		let angle=0;
		let graphic=RED12;
		let delay=10;
		let fr=[25,20,15,10];

		ascent(let i in 0 .. num_[Lv]){
			SetShotDirectionType(PLAYER);
			RoundWideLineShot(GetX,GetY,minSpeed[Lv],maxSpeed[Lv],angle,wideAngle[Lv],wideWay[Lv],roundWay,num[Lv],graphic,delay);
			_Wait(fr[Lv]);
		}

	}

	// 移動 --------------------------------------------------------------------
	function move(let x, let y, let fr){

		x+=RandBlur(15);
		y+=RandBlur(15);
		let power=GetGapLength(GetX,GetY,x,y)*2;
		let angle=GetGapAngle(GetX,GetY,x,y);
		SetMovePositionHermite(x,y,power,angle,0,0,fr);

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		let x=0;
		let y=0;
		let sLength=128;
		SetAlpha(255);
		SetColor(255,255,255);
		_DrawChrRectSqr(imgBoss,GetX,GetY,x,y,sLength);

	}

}
