script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"


//変数宣言--------------------------------------
	let count=0;
	let Level = GetCommonDataDefault( "Level", 1 );
//タスクの作成----------------------------------

task MainAction()
{
	yield;
	_Wait(125);

	let angle_b=GetAngleToPlayer();
	let speed=[1,1.2,2.5,4][Level];
	while(speed>=[1,1.2,1.2,1][Level])
	{
		let angle=-180;
		while(angle<180)
		{
			CreateShotA(0,GetX(),GetY(),20);
			SetShotDataA(0,0,speed,angle+angle_b,0,-[0.02,0.03,0.03,0.065][Level],0,[RED23,GREEN23,BLUE23,YELLOW23,PURPLE23,AQUA23,ORANGE23][int(rand(0,6))]);
			SetShotDataA(0,100,speed,angle+angle_b+90,2.5+rand(-0.1,0.1),0,0,[RED23,GREEN23,BLUE23,YELLOW23,PURPLE23,AQUA23,ORANGE23][int(rand(0,6))]);
			SetShotDataA(0,230,[1,1.2,1.5,2][Level],angle+angle_b+180,0,0,0,[RED23,GREEN23,BLUE23,YELLOW23,PURPLE23,AQUA23,ORANGE23][int(rand(0,6))]);
			FireShot(0);
			angle+=[12,10,10,10][Level];
		}
		speed=speed/1.2;
	}
}
task SubAction01()
{
	yield;
	let spmax=GetSpeed();
	let i=0;
	loop(50)
	{
		SetSpeed(SlowDown( spmax , 0 , i , 50 ));
		i++;
		yield;
	}
	_Wait(100);
	let XX=GetX+rand(-100,100);
	SetMovePositionHermite(XX,GetClipMinY()-50,6*(XX-GetX()),0,100,-90,200);
}

//実行部分---------------------------------------

	@Initialize
	{
		SetLife(13);
		SetInvincibility( 130 );
		MainAction();
		SubAction01();
	}
	
	@MainLoop
	{
		Gone();
		Deletemine();
		yield;
		count++;
		//敵画像代わり------------------------------------
		if(int(count/2)==count/2)
		{
			CreateShotA(0,GetX(),GetY(),0);
			SetShotDataA(0,0,0,GetAngle(),0,0,0,RED22);
			SetShotKillTime(0,2);
			FireShot(0);
		}
		//------------------------------------------------
		Collision( 20, 8 );
	}
	@DrawLoop
	{
	}
	@Finalize
	{
		DropItem( 5 , 40 );
	}
}
