script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"
#include_function ".\index_UserShot.dnh"

//変数宣言--------------------------------------
	let count=0;
	let Level = GetCommonDataDefault( "Level", 1 );

//タスクの作成----------------------------------

task MainAction()
{
	yield;
	_Wait(125);
	let angle=GetAngleToPlayer()-90;

	loop([3,6,9,15][Level])
	{

		let speed=[1.5,2,2.6,3.5][Level];

		let time=rand(50,80);
		while(speed>1)
		{
			CreateShotA(0,GetX(),GetY(),30);
			SetShotDataA(0,0,speed,angle,0,0,0,1);
			SetShotDataA(0,time,NULL,angle+180,0,0,0,1);
			SetShotDirectionType(SEQUENCE);
			SetShotDataA(0,time+70,speed,180-30,sin(angle)/10,0,0,1);
			FireShot(0);
			SetShotDirectionType(ABSOLUTE);
			CreateShotA(0,GetX(),GetY(),30);
			SetShotDataA(0,0,speed,angle,0,0,0,1);
			SetShotDataA(0,time,NULL,angle+180,0,0,0,1);
			SetShotDirectionType(SEQUENCE);
			SetShotDataA(0,time+70,speed,180+30,-sin(angle)/10,0,0,1);
			FireShot(0);
			SetShotDirectionType(ABSOLUTE);
			speed=speed-0.3
		}
		angle+=180/([3,6,9,15][Level]-1);
	}

}
task SubAction01()
{
	yield;
	let spmax=GetSpeed();
	let i=0;
	loop(50)
	{
		SetSpeed(SlowDown( spmax , 0 , i , 50 ));
		i++;
		yield;
	}
	_Wait(100);
	let XX=GetX+rand(-100,100);
	SetMovePositionHermite(XX,GetClipMinY()-50,6*(XX-GetX()),0,100,-90,200);
}

//実行部分---------------------------------------

	@Initialize
	{
		SetLife(13);
		SetInvincibility( 130 );
		MainAction();
		SubAction01();
		LoadUserShotData(shotMedicine);
	}
	
	@MainLoop
	{
		Gone();
		Deletemine();
		yield;
		count++;
		//敵画像代わり------------------------------------
		if(int(count/2)==count/2)
		{
			CreateShotA(0,GetX(),GetY(),0);
			SetShotDataA(0,0,0,GetAngle(),0,0,0,RED22);
			SetShotKillTime(0,2);
			FireShot(0);
		}
		//------------------------------------------------
		Collision( 20, 8 );
	}
	@DrawLoop
	{
	}
	@Finalize
	{
		DropItem( 5 , 40 );
	}
}
