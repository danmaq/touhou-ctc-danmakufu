#東方弾幕風
#Title[Remilia - 通常攻撃2]
#Text[ステージ2A ボス
レミリア・スカーレット

下記以外のスタート＝Easy
スキップ＋スタート＝Normal
ユーザー＋スタート＝Hard
ｽｷｯﾌﾟ＋ﾕｰｻﾞｰ＋ｽﾀｰﾄ＝Lunatic]
#Image[]
#BackGround[Default]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\index.dnh"
	let imgBoss=dotBossSample;
	let imgEnemy=dotCircle6s;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);
		LoadGraphic(imgEnemy);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetTimer(38);
		SetShotAutoDeleteClip(32,32,32,32);
		SetEnemyMarker(true);

		// ボスステータス設定（ライフ、ダメージ率Shot%,Bomb%等）
		SetLife(2500);
		SetDamageRate(100,50);

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（A:自弾,B:体当たり）
		SetCollisionA(GetX,GetY,32);
		SetCollisionB(GetX,GetY,24);

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(182);
		wait(2);

		// 初期移動
		let fr=180;
		let x=cenX;
		let y=minY+120+rand(-1,1);
		SetMovePosition02(x,y,fr);
		wait(fr);

		let assign=-1;

		loop{

			// 使い魔召喚
			SummonEnemy(assign);
			wait(60);

			loop(2){

				// 移動
				move(60);
				wait(60);

				// 扇状ばらつき弾
				WideVariedShot(assign);
				wait(60);

			}

			// 移動
			move(90);
			wait(120);

			assign*=-1;

		}

	}

	// 使い魔召喚 --------------------------------------------------------------
	task SummonEnemy(let assign){

		let way=4+2*Lv;
		let gapLength=80;
		let wideAngle=160;
		let wayAngle=wideAngle/(way-1)*assign;
		let baseAngle=90-wayAngle*(way-1)/2;
		let fr=5;

		ascent(let i in 0 .. way){
			let angle=baseAngle+wayAngle*i;
			let x=GetGapX(GetX,gapLength,angle);
			let y=GetGapY(GetY,gapLength,angle);
			CreateEnemyFromScript("Enemy",x,y,0,angle,0);
			wait(fr);
		}

	}

	// 扇状ばらつき弾 ----------------------------------------------------------
	task WideVariedShot(let assign){

		let way=5+2*Lv;
		let wideAngle=160;
		let wayAngle=-wideAngle/(way-1)*assign;
		let baseAngle=GetAngleToPlayer-wayAngle*(way-1)/2;
		let fr=5;

		ascent(let i in 0 .. way){
			let angle=baseAngle+wayAngle*i;
			VariedShot(angle);
			wait(fr);
		}

	}

	// ばらつき弾 --------------------------------------------------------------
	function VariedShot(let baseAngle){

		let num=21;
		let baseSpeed=1;
		let numSpeed=(4.2-baseSpeed)/(num-1);
		let graphic=[RED05,RED01,RED02,RED03];
		let gra=0;
		let delay=15;

		SetShotDirectionType(ABSOLUTE);
		ascent(let i in 0 .. num){
			let speed=baseSpeed+numSpeed*i;
			if(i==7||i==14||i==20){gra++;}
			let angle=baseAngle+rand(-4,4);
			if(i==num-1){angle=baseAngle;}
			CreateShot01(GetX,GetY,speed,angle,graphic[gra],delay);
		}

	}

	// 移動 --------------------------------------------------------------------
	function move(let fr){

		let x=GetX;
		let y=GetY;
		let mx=rand(40,80);
		let my=rand(0,20);
		if(x>=GetPlayerX){
			if(x-mx>=minX+100){mx=-mx;}
		}
		else{
			if(x+mx>=maxX-100){mx=-mx;}
		}
		if(y>=minY+120){my=-my;}
		x+=mx;
		y+=my;
		let power=GetGapLength(GetX,GetY,x,y)*2;
		let angle=GetGapAngle(GetX,GetY,x,y);
		SetMovePositionHermite(x,y,power,angle,0,0,fr);

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);
		DeleteGraphic(imgEnemy);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		SetTexture(imgBoss);
		if(GetSpeed==0){SetGraphicRect(1,1,64,64);}
		else if(GetAngle>-90&&GetAngle<=90){SetGraphicRect(192,1,255,64);}
		else {SetGraphicRect(128,1,191,64);}
		SetAlpha(255);
		SetColor(255,255,255);
		DrawGraphic(GetX,GetY);

	}

	// 共通関数読み込み
	#include_function ".\INCLUDE\patternShot.dnh"
	#include_function ".\INCLUDE\library_request.dnh"

}

// 使い魔 ----------------------------------------------------------------------
script_enemy Enemy{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\DOT\index.dnh"
	let imgEnemy=dotCircle6s;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	let Lv=GetCommonDataDefault("Level",0);
	let drawAngle=0;
	let x_=GetX;
	let y_=GetY;

	// -------------------------------------------------------------------------
	@Initialize{

		// ステータス設定（ライフ、自ダメ率Shot%,Bomb%,親ダメ率Shot%,Bomb%等）
		SetLife(200);
		SetDamageRateExDelay(30,100,50,50,50);
		SetX(GetEnemyX);
		SetY(GetEnemyY);

		// 光
		Light;

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 回転
		drawAngle+=2;

		// 当たり判定（A:自弾,B:体当たり）
		if(GetKeyState(VK_SLOWMOVE)==KEY_PULL||GetKeyState(VK_SLOWMOVE)==KEY_FREE){
			SetCollisionA(GetX,GetY,24);
			SetCollisionB(GetX,GetY,18);
		}

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		// 移動
		SetColor(255,31,31);
		Concentration01(30);
		let power=GetGapLength(GetX,GetY,x_,y_)*2;
		let angle=GetGapAngle(GetX,GetY,x_,y_);
		SetMovePositionHermite(x_,y_,power,angle,0,0,40);
		wait(60);

		// ばらまき弾
		angle=GetAngle;
		if(GetPlayerY<GetY){angle=GetGapAngle(GetX,GetY,GetPlayerX,GetPlayerY);}
		VariedShot(angle);
		wait(120);
		angle=GetAngle;
		if(GetPlayerY<GetY){angle=GetGapAngle(GetX,GetY,GetPlayerX,GetPlayerY);}
		VariedShot(angle);
		wait(60);

		// 移動
		SetSpeed(2);
		wait(240);

		VanishEnemy;

	}

	// ばらまき弾 --------------------------------------------------------------
	function VariedShot(let baseAngle){

		let num=21;
		let baseSpeed=1;
		let numSpeed=(4-baseSpeed)/(num-1);
		let graphic=[RED05,RED01,RED02,RED03];
		let gra=0;
		let delay=15;

		SetShotDirectionType(ABSOLUTE);
		ascent(let i in 0 .. num){
			let speed=baseSpeed+numSpeed*i;
			if(i==7||i==15||i==20){gra++;}
			let angle=baseAngle+rand(-3.5,3.5);
			if(i==num-1){angle=baseAngle;}
			CreateShot01(GetX,GetY,speed,angle,graphic[gra],delay);
		}

	}

	// 光 ----------------------------------------------------------------------
	task Light{

		loop{
			if(GetKeyState(VK_SLOWMOVE)==KEY_PULL||GetKeyState(VK_SLOWMOVE)==KEY_FREE){
				Light_(WHITE01);
			}
			wait(3);
		}

	}

	task Light_(let graphic){

		// 初期設定 ----------------------------------------
		let obj=Obj_Create(OBJ_SHOT);
		let delay=15;

		Obj_SetX(obj,GetX);
		Obj_SetY(obj,GetY);
		ObjShot_SetDelay(obj,delay);
		ObjShot_SetGraphic(obj,graphic);
		Obj_SetAlpha(obj,0);
		Obj_SetCollisionToPlayer(obj,false);
		ObjShot_SetBombResist(obj,true);

		// メイン処理 --------------------------------------
		loop(delay){
			if(GetKeyState(VK_SLOWMOVE)==KEY_PUSH||GetKeyState(VK_SLOWMOVE)==KEY_HOLD){
				break;
			}
			yield;
		}
		Obj_Delete(obj);

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@Finalize{

		if(GetLife<=0){
			DeleteEnemyShotToItemInCircle(ALL,GetX,GetY,40);
		}

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// 本体描画
		SetTexture(imgEnemy);
		SetGraphicRect(64,0,128,64);
		if(GetKeyState(VK_SLOWMOVE)==KEY_PUSH||GetKeyState(VK_SLOWMOVE)==KEY_HOLD){
			SetAlpha(127);
		}
		else{
			SetAlpha(255);
		}
		SetColor(255,255,255);
		SetGraphicScale(0.8,0.8);
		SetGraphicAngle(0,0,drawAngle);
		DrawGraphic(GetX,GetY);

	}

	// 共通関数読み込み
	#include_function ".\INCLUDE\patternShot.dnh"
	#include_function ".\INCLUDE\library_request.dnh"

}
