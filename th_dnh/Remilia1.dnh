#東方弾幕風
#Title[Remilia - 通常攻撃1]
#Text[ステージ2A ボス
レミリア・スカーレット

下記以外のスタート＝Easy
スキップ＋スタート＝Normal
ユーザー＋スタート＝Hard
ｽｷｯﾌﾟ＋ﾕｰｻﾞｰ＋ｽﾀｰﾄ＝Lunatic]
#Image[]
#BackGround[Default]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\index.dnh"
	let imgBoss=dotBossSample;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetTimer(37);
		SetShotAutoDeleteClip(32,32,32,32);
		SetEnemyMarker(true);

		// ボスステータス設定（ライフ、ダメージ率Shot%,Bomb%等）
		SetLife(2500);
		SetDamageRate(100,50);
		SetX(cenX);
		SetY(minY+120+rand(-1,1));

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（A:自弾,B:体当たり）
		SetCollisionA(GetX,GetY,32);
		SetCollisionB(GetX,GetY,24);

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(62);
		wait(2);

		// 発動
		ExplosionBoss;
		wait(60);

		loop{

			// 破裂弾
			Concentration01(30);
			wait(30);
			CrackShot;
			wait(45);

			// クロス直線弾
			let assign=1;
			let ly=rand(20,40);
			if(GetX<cenX){assign=-1;}
			CrossLineShot(ly,assign);
			wait(30);
			CrossLineShot(ly,-assign);
			wait(75);

			// 移動
			move(90);
			wait(90);

		}

	}

	// 破裂弾 ------------------------------------------------------------------
	task CrackShot{

		// 初期設定 ----------------------------------------
		let obj=Obj_Create(OBJ_SHOT);
		let fr=15;
		let speed=(GetY-minY)/fr;
		let graphic=RED03;

		Obj_SetX(obj,GetX);
		Obj_SetY(obj,GetY);
		Obj_SetSpeed(obj,speed);
		Obj_SetAngle(obj,-90);
		ObjShot_SetGraphic(obj,graphic);

		// メイン処理 --------------------------------------
		loop(fr){
			if(Obj_BeDeleted(obj)){break;}
			yield;
		}
		if(!Obj_BeDeleted(obj)){
			let way=11+2*Lv;
			let num=6;
			let minSpeed=2.5;
			let maxSpeed=5;
			let wayAngle=180/(way-1);
			way+=2;
			let wideAngle=180+wayAngle*2;
			let angle=90+rand(-wayAngle,wayAngle);
			SetShotDirectionType(ABSOLUTE);
			WideLineShot(Obj_GetX(obj),Obj_GetY(obj),minSpeed,maxSpeed,angle,wideAngle,way,num,graphic,0);
			Obj_Delete(obj);
		}

	}

	// クロス直線弾 ------------------------------------------------------------
	task CrossLineShot(
		let ly,
		let assign
	){

		let num=3+Lv;
		let way=32+4*Lv;
		let lx=cenX-minX;
		let lineX1=cenX-lx*assign;
		let lineX2=cenX+lx*assign;
		let gapX=(lineX2-lineX1)/(way-1);
		let lineY1=cenY+ly;
		let lineY2=cenY-ly;
		let gapY=(lineY2-lineY1)/(way-1);
		let graphic=RED32;
		let delay=10;
		let fr=1;

		ascent(let i in 0 .. way){
			let x=lineX1+gapX*i;
			let y=lineY1+gapY*i;
			let minSpeed=GetGapLength(GetX,GetY,x,y)/120;
			let maxSpeed=minSpeed*(2.4+0.4*Lv);
			let angle=GetGapAngle(GetX,GetY,x,y);
			SetShotDirectionType(ABSOLUTE);
			LineShot(GetX,GetY,minSpeed,maxSpeed,angle,num,graphic,delay);
			wait(fr);
		}

	}

	// 移動 --------------------------------------------------------------------
	function move(let fr){

		let x=GetX;
		let y=GetY;
		let mx=rand(30,60);
		let my=rand(0,20);
		if(x>=GetPlayerX){
			if(x-mx>=minX+100){mx=-mx;}
		}
		else{
			if(x+mx>=maxX-100){mx=-mx;}
		}
		if(y>=minY+120){my=-my;}
		x+=mx;
		y+=my;
		let power=GetGapLength(GetX,GetY,x,y)*2;
		let angle=GetGapAngle(GetX,GetY,x,y);
		SetMovePositionHermite(x,y,power,angle,0,0,fr);

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		SetTexture(imgBoss);
		if(GetSpeed==0){SetGraphicRect(1,1,64,64);}
		else if(GetAngle>-90&&GetAngle<=90){SetGraphicRect(192,1,255,64);}
		else {SetGraphicRect(128,1,191,64);}
		SetAlpha(255);
		SetColor(255,255,255);
		DrawGraphic(GetX,GetY);

	}

	// 共通関数読み込み
	#include_function ".\INCLUDE\patternShot.dnh"
	#include_function ".\INCLUDE\library_request.dnh"

}
