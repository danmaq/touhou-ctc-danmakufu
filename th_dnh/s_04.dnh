script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"

//変数宣言--------------------------------------
	let count=0;
	let Level=GetCommonData("Level");
//タスクの作成----------------------------------
task MainAction()
{
	yield;
	_Wait(rand(10,40));
	while( GetY() < GetCenterY()+40 )
	{
		if(Level != 0)
		{
			loop([0,8,15,20][Level])
			{
				shotobjA (rand([0,0.6,1,2][Level],[0,2,3,4][Level]), rand(0,360));
			}
		}
		_Wait([999,100,80,60][Level]+int(rand(-10,10)));
	}
}

//実行部分---------------------------------------

	@Initialize
	{
		SetLife(30);
		SetInvincibility( 10 );
		SetAngle( GetAngle() );
		MainAction();
	}
	
	@MainLoop
	{
		Gone();
		Deletemine();
		yield;
		Collision(16 , 12);
		count++;
		//敵画像代わり------------------------------------
		if(int(count/2)==count/2)
		{
			CreateShotA(0,GetX(),GetY(),0);
			SetShotDataA(0,0,0,GetAngle(),0,0,0,RED22);
			SetShotKillTime(0,2);
			FireShot(0);
		}
		//------------------------------------------------

	}
	@DrawLoop
	{
	}
	@Finalize
	{
		DropItem( 1 , 20 );
		DeleteEnemyShot(CHILD);
	}




	task shotobjA (let speed, let angle)
	{
		//弾タスク初期化
		let obj = Obj_Create(OBJ_SHOT);//弾オブジェクトを作成
		Obj_SetX(obj, GetX());//x座標設定
		Obj_SetY(obj, GetY());//y座標設定;
		Obj_SetSpeed(obj, speed);//速度設定
		Obj_SetAngle(obj, angle);//移動角度設定
		ObjShot_SetGraphic(obj, PURPLE23 );//画像設定
		ObjShot_SetDelay(obj,0);//遅延時間設定

		//変数宣言

		let count=0;
		let alpha=0;
		
		while( !Obj_BeDeleted(obj) )
		{//削除されるまでループを実行

			alpha=Smooth( 255 ,150,count, 150 );
			Obj_SetSpeed(obj, Smooth( speed ,0,count, 70 ));
			Obj_SetAlpha(obj,alpha);
			if(count==150)
			{ObjShot_FadeDelete(obj);}

			if(Obj_GetX(obj)<=GetClipMinX()-10 || Obj_GetX(obj)>=GetClipMaxX()+10 || Obj_GetY(obj)<=GetClipMinY()-10 || Obj_GetY(obj)>=GetClipMaxY()+10)
			{
				Obj_Delete(obj);
				break;
			}//画面外に出たら消える
			count++;
			yield;

		}
	}


}
