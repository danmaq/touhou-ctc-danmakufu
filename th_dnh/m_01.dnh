script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"

//変数宣言--------------------------------------
	let count=0;
	let Level=GetCommonData("Level");
	let angle_b=GetAngleToPlayer();
//タスクの作成----------------------------------
task MainAction()
{
	yield;
	_Wait(50);
	
	let angle=-20;
	while(angle<=20)
	{
		CreateEnemyFromScript("t_ma_01",GetX(),GetY(),1.2,GetAngleToPlayer()+angle,0);
		angle+=10;
	}
	_Wait(40);
	let angle=-[40,70,80,80][Level];
	while(angle <= [20,40,60,90][Level])
	{
		CreateShot01(GetX(),GetY(),[1.3,1.7,2.2,3][Level],angle+angle_b,BLUE04,0);
		if(Level > 1)
		{
			CreateShot01(GetX(),GetY(),[0,0,2.4,3.3][Level],-angle+angle_b+[0,0,4,3][Level],RED04,0);
		}

		if(Level==3)
		{
			CreateShot01(GetX(),GetY(),[0,0,0,2.8][Level],angle+angle_b,BLUE04,0);
			CreateShot01(GetX(),GetY(),[0,0,0,3.1][Level],-angle+angle_b+[0,0,4,3][Level],RED04,0);
		}

		angle+=[12,12,10,10][Level];
		loop(2){yield;}
	}
	_Wait(60);

}
task SubAction01()
{
	yield;
	let spmax=GetSpeed();
	let i=0;
	loop(50)
	{
		SetSpeed(SlowDown( spmax , 0 , i , 50 ));
		i++;
		yield;
	}
	_Wait(110);
	let XX=GetX+rand(-100,100);
	SetMovePositionHermite(XX,GetClipMinY()-50,6*(XX-GetX()),0,100,-90,400);


}

//実行部分---------------------------------------

	@Initialize
	{
		SetLife(30);
		SetInvincibility( 120 );
		MainAction();
		SubAction01();
	}
	
	@MainLoop
	{
		Gone();
		Deletemine();
		yield;
		Collision(16 , 12);
		count++;
		//敵画像代わり------------------------------------
		if(int(count/2)==count/2)
		{
			CreateShotA(0,GetX(),GetY(),0);
			SetShotDataA(0,0,0,GetAngle(),0,0,0,RED22);
			SetShotKillTime(0,2);
			FireShot(0);
		}
		//------------------------------------------------

	}
	@DrawLoop
	{
	}
	@Finalize
	{
		DropItem( 2 , 20 );
	}
}

////////////////////////////////////////////////////////////////////////
//仮の使い魔、使い魔の書式の統一が必要なので雛形の作成をお願いします
////////////////////////////////////////////////////////////////////////

script_enemy t_ma_01
{

#include_function ".\INCLUDE\Shadow.dnh"

	let count=0;
	let scale=0;

	@Initialize
	{	
		SetLife(30);
		LoadGraphic(dotCircle6s);
	}
	@MainLoop
	{
		Deletemine();
		Gone();
		BehaviorShadow( 0,0 );
		count++;
		scale=Smooth( 0,1,count,40 );
	
		if(GetKeyState(VK_SLOWMOVE)==KEY_FREE)
		{
			Collision( 16,12 );
			if(int(count/3)==count/3)
			{
				CreateShotA(0,GetX(),GetY(),10);
				SetShotDataA(0,0,0,0,0,0,0,WHITE05);
				SetShotKillTime(0,0);
				FireShot(0);
			}
		}
	}
	@DrawLoop
	{

		SetTexture(dotCircle6s);
		SetGraphicRect(0,0,64,64);
		if(GetKeyState(VK_SLOWMOVE)==KEY_PUSH||GetKeyState(VK_SLOWMOVE)==KEY_HOLD){
			SetAlpha(127);
			SetGraphicScale(0.8,0.8);
		}
		else{
			SetAlpha(255);
			SetGraphicScale(0.5,0.5);
		}
		SetColor(255,255,255);
		SetGraphicAngle(0,0,count*2);
		DrawGraphic(GetX,GetY);


	}
	@Finalize
	{
		DestructShadow( 40 );
	}
}
