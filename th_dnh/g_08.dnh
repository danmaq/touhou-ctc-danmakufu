script_enemy_main
{

#include_function ".\INCLUDE\Enemy.dnh"


//変数宣言--------------------------------------
	let count=0;
	let Level = GetCommonDataDefault( "Level", 1 );
//タスクの作成----------------------------------

task MainAction()
{
	yield;
	_Wait([140,50][GetArgument()]);
	while(1)
	{
		let angle_b=-180+rand(1,30);
		while(angle_b<180)
		{
			let angle=-32;
			while(angle<32)
			{
				CreateShotA(0,GetX(),GetY(),20);
				SetShotDataA(0,0,3-absolute(angle/15),angle+angle_b,0,-0.03,0,ORANGE23);
				SetShotDirectionType(SEQUENCE);
				SetShotDataA(0,120,0.1,[90,-90][angle<=0],absolute(angle/10),0,0,ORANGE23);
				SetShotDataA(0,180,[0.6,1,1.4,1.8][Level],[90,-90][angle<=0],0,0,0,ORANGE23);
				SetShotDirectionType(ABSOLUTE);
				FireShot(0);
				angle+=1.5;
			}
			angle_b+=[120,60,45,30][Level];
		}
		_Wait(70);
	}

}
task SubAction01()
{
	yield;
	let spmax=GetSpeed();
	let i=0;
	loop([100,45][GetArgument()])
	{
		SetSpeed(SlowDown( spmax , 0 , i , [100,45][GetArgument()] ));
		i++;
		yield;
	}
}

//実行部分---------------------------------------

	@Initialize
	{
		SetLife(2000);
		MainAction();
		SubAction01();
	}
	
	@MainLoop
	{
		Gone();
		Deletemine();
		yield;
		count++;
		if(count==650){DropItem( 20 , 20 );VanishEnemy();}
		//敵画像代わり------------------------------------
		if(int(count/2)==count/2)
		{
			CreateShotA(0,GetX(),GetY(),0);
			SetShotDataA(0,0,0,GetAngle(),0,0,0,RED22);
			SetShotKillTime(0,2);
			FireShot(0);
		}
		//------------------------------------------------
		Collision( 20, 8 );
	}
	@DrawLoop
	{
	}
	@Finalize
	{
		DropItem( 20 , 20 );
	}
}
