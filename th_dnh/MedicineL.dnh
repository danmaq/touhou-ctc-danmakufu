#東方弾幕風
#Title[Medicine - ラストスペル]
#Text[ステージ1C ボス
メディスン・メランコリー


下記以外のスタート＝Normal
ユーザー＋スタート＝Hard
ｽｷｯﾌﾟ＋ﾕｰｻﾞｰ＋ｽﾀｰﾄ＝Lunatic]
#Image[]
#BackGround[Default]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\index.dnh"
	#include_function ".\index_UserShot.dnh"
	let imgBoss=dotBossSample;
	let imgShot=dotPoisonShot;
	let userShot=shotMedicine;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	// 難易度
	Debug_SetLevel;
	let Lv=GetCommonDataDefault("Level",0);
	if(Lv==0){Lv=1;}
	Lv--;

	let alpha=0;

	// -------------------------------------------------------------------------
	@Initialize{

		// 画像及び効果音をロード
		LoadGraphic(imgBoss);
		LoadGraphic(imgShot);
		LoadUserShotData(userShot);

		// スペルカード設定（カード名、スコア、制限時間、弾削除範囲等）
		SetTimer(65);
		SetShotAutoDeleteClip(20,20,20,20);
		SetEnemyMarker(true);
		LastSpell;

		// ボスステータス設定（ライフ、ダメージ率Shot%,Bomb%等）
		SetLife(500);
		SetDamageRate(15,0);
		SetX(cenX);
		SetY(minY+120+rand(-1,1));
		LastSpellEffect(120,500);

		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		// 当たり判定（A:自弾,B:体当たり）
		if(alpha>0){
			SetCollisionA(GetX,GetY,32);
			SetCollisionB(GetX,GetY,24);
		}

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		SetInvincibility(242);
		wait(122);

		// フェードイン
		FadeIn(60);
		SetText("＊符「＊＊＊」");
		SetScore(100000);
		wait(120);

		// ランダム毒霧
		RandomPoisonShot;
		wait(60);

		loop{

			wait(60);

			// 隠れ弾
			HiddenShot(-1);
			HiddenShot(1);
			wait(60);

			// 移動
			move(120);
			wait(60);

			// 隠れ弾
			HiddenShot(-1);
			HiddenShot(1);
			wait(60);

		}

	}

	// 隠れ弾 ------------------------------------------------------------------
	task HiddenShot(let assign){

		let gapLength=70;
		let gapAngle=GetGapAngle(GetX,GetY,cenX,cenY)-90*assign;
		let x=GetGapX(GetX,gapLength,gapAngle);
		let y=GetGapY(GetY,gapLength,gapAngle);
		let way=[19,22,30,32];
		let num=8;
		let minSpeed=1.5;
		let maxSpeed=3.5;
		let baseAngle=gapAngle-165*assign;
		let wayAngle=330/(way[Lv]-1)*assign;
		let graphic=1;
		let delay=15;
		let fr=3;

		ascent(let i in 0 .. way[Lv]){
			let angle=baseAngle+wayAngle*i;
			SetShotDirectionType(ABSOLUTE);
			LineShot(x,y,minSpeed,maxSpeed,angle,num,graphic,delay);
			wait(fr);
		}

	}

	// ランダム毒霧 ------------------------------------------------------------
	task RandomPoisonShot{

		let rX=[minX,minX,maxX,maxX];
		let rY=[minY,maxY,maxY,minY];
		let gapLength=10;
		let speed=5;
		let fr=7;

		loop{
			ascent(let i in 0 .. 4){
				let x=rand(cenX,rX[i]);
				let y=rand(cenY,rY[i]);
				let angle=GetGapAngle(GetX,GetY,x,y);
				let seedFr=trunc(GetGapLength(GetX,GetY,x,y)/speed);
				PoisonSeed(GetX,GetY,speed,angle,seedFr);
				wait(fr);
			}
		}

	}

	// 毒霧弾 ------------------------------------------------------------------
	task PoisonSeed(
		let x,
		let y,
		let speed,
		let angle,
		let fr
	){

		// 初期設定 ----------------------------------------
		let obj=Obj_Create(OBJ_SHOT);
		let delay=0;

		Obj_SetX(obj,x);
		Obj_SetY(obj,y);
		Obj_SetSpeed(obj,speed);
		Obj_SetAngle(obj,angle);
		ObjShot_SetDelay(obj,delay);
		ObjShot_SetGraphic(obj,BLUE02);
		ObjShot_SetBombResist(obj,true);

		// メイン処理 --------------------------------------
		loop(delay+fr){
			if(Obj_BeDeleted(obj)){break;}
			yield;
		}
		if(!Obj_BeDeleted(obj)){
			CreateShotFromScript("PoisonMist",Obj_GetX(obj),Obj_GetY(obj),0,rand(0,360),0,0);
			Obj_Delete(obj);
		}

	}

	// 移動 --------------------------------------------------------------------
	function move(let fr){

		let x=GetX;
		let y=GetY;
		let mx=rand(40,80);
		let my=rand(0,20);
		if(x>=GetPlayerX){
			if(x-mx>=minX+100){mx=-mx;}
		}
		else{
			if(x+mx>=maxX-100){mx=-mx;}
		}
		if(y>=minY+110){my=-my;}
		x+=mx;
		y+=my;
		SetMovePosition02(x,y,fr);

	}

	// ラストスペルエフェクト --------------------------------------------------
	task LastSpellEffect(
		let fr,
		let maxLife
	){

		let disLife=maxLife-1;
		if(disLife>0){AddLife(-disLife);}
		Concentration01(fr);
		wait(fr);
		if(disLife>0){
			fr=60;
			let addLife=maxLife/fr;
			loop(fr){
				AddLife(addLife);
				yield;
			}
		}

	}

	// フェードイン ------------------------------------------------------------
	task FadeIn(let fr){

		let addAlpha=255/fr;
		loop(fr){
			alpha+=addAlpha;
			yield;
		}

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@Finalize{

		// ロードしたファイルを削除
		DeleteGraphic(imgBoss);
		DeleteGraphic(imgShot);

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// ボス描画
		SetTexture(imgBoss);
		if(GetSpeed==0){SetGraphicRect(1,1,64,64);}
		else if(GetAngle>-90&&GetAngle<=90){SetGraphicRect(192,1,255,64);}
		else {SetGraphicRect(128,1,191,64);}
		SetAlpha(alpha);
		SetColor(255,255,255);
		DrawGraphic(GetX,GetY);

	}

	// 共通関数読み込み
	#include_function ".\INCLUDE\patternShot.dnh"
	#include_function ".\INCLUDE\library_request.dnh"

}

// 毒霧 ------------------------------------------------------------------------
script_shot PoisonMist{

	// 画像及び効果音ファイル
	#include_function ".\IMAGE\CHAR\DOT\index.dnh"
	let imgShot=dotPoisonShot;

	// 座標設定
	let cenX=GetCenterX;
	let cenY=GetCenterY;
	let minX=GetClipMinX;
	let maxX=GetClipMaxX;
	let minY=GetClipMinY;
	let maxY=GetClipMaxY;

	let scale=0.15;

	// -------------------------------------------------------------------------
	@Initialize{

		SetBombResist;
		Main;

	}

	// -------------------------------------------------------------------------
	@MainLoop{

		yield;

	}

	// メイン処理 --------------------------------------------------------------
	task Main{

		yield;

		// 拡大
		let fr=160;
		let maxScale=1;
		let addScale=(maxScale-scale)/fr;
		loop(fr){
			scale+=addScale;
			yield;
		}

		// 消滅
		fr=20;
		addScale=-scale/fr;
		loop(fr){
			scale+=addScale;
			yield;
		}
		End;

	}

	// 待ち --------------------------------------------------------------------
	function wait(let fr){

		loop(fr){yield;}

	}

	// -------------------------------------------------------------------------
	@DrawLoop{

		// 弾描画
		SetTexture(imgShot);
		SetGraphicRect(0,0,256,256);
		SetRenderState(ADD);
		SetColor(255,255,255);
		SetGraphicScale(scale,scale);
		DrawGraphic(GetX,GetY);

	}

}
